#!/usr/bin/perl

# Copyright: 2003 Petter Reinholdtsen <pere@hungry.com>
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions
# are met:
# 1. Redistributions of source code must retain the above copyright
#    notice, this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright
#    notice, this list of conditions and the following disclaimer in the
#    documentation and/or other materials provided with the distribution.
# .
# THIS SOFTWARE IS PROVIDED BY AUTHORS AND CONTRIBUTORS ``AS IS'' AND
# ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHORS OR CONTRIBUTORS BE LIABLE
# FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
# OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
# HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
# LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
# OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
# SUCH DAMAGE.

=head1 NAME

debconf-get-selections - output contents of debconf database

=head1 SYNOPSIS

debconf-get-selections [--installer]

=head1 DESCRIPTION

Output the current debconf database in a format understandable by
debconf-set-selections.

To dump the debconf database of the debian-installer, from
/var/log/installer/cdebconf, use the --installer
parameter.

=cut

use strict;
use warnings;
use Debconf::Db;
use Debconf::Template;
use Debconf::Question;

Debconf::Db->load(readonly => "true");

my $defaultowner="unknown";

if (@ARGV && $ARGV[0] eq '--installer') {
	# A bit of a hack..
	my $di_path;
	if (-d "/var/log/installer") {
		$di_path="/var/log/installer/cdebconf";
	} else {
		$di_path="/var/log/debian-installer/cdebconf";
	}
	$Debconf::Db::config=Debconf::Db->makedriver(
		driver => "File", 
		name => "di_questions",
		filename => "$di_path/questions.dat",
		readonly => "true",
	);
	$Debconf::Db::templates=Debconf::Db->makedriver(
		driver => "File", 
		name => "di_templates",
		filename => "$di_path/templates.dat",
		readonly => "true",
	);
	$defaultowner="d-i";
}

my $qi = Debconf::Question->iterator;

while (my $q = $qi->iterate) {
	my ($name, $type, $value) = ($q->name, $q->type, $q->value);
	next if (! length $type || $type eq 'text' || $type eq 'title');
	print "# ".$q->description."\n";
	if ($q->type eq 'select' || $q->type eq 'multiselect') {
		print "# Choices: ".join(", ", $q->choices)."\n";
	}
	if ($q->owners) {
		foreach my $owner (split ", ", $q->owners) {
			print "$owner\t$name\t$type\t$value\n";
		}
	}
	else {		
		print "$defaultowner\t$name\t$type\t$value\n";
	}
}

=head1 AUTHOR

Petter Reinholdtsen <pere@hungry.com>

=cut
